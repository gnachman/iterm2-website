set -l fishversion (echo $FISH_VERSION | sed 's|\(.\)\.\(.\).*|\1\2|')
if [ $fishversion -ge 22 ]
  if [ x"$TERM" != "xscreen" ]

    function iterm2_status -e fish_postexec
      printf "\033]133;D;%s\007" $status
    end

    # Mark start of prompt
    function iterm2_prompt_start
      printf "\033]133;A\007"
    end

    # Mark end of prompt
    function iterm2_prompt_end
      printf "\033]133;B\007"
    end

    # Tell terminal to create a mark at this location
    function iterm2_preexec -e fish_preexec
      printf "\033]133;C\007"
    end

    # Usage: iterm2_set_user_var key value
    # These variables show up in badges (and later in other places). For example
    # iterm2_set_user_var currentDirectory "$PWD"
    # Gives a variable accessible in a badge by \(user.currentDirectory)
    # Calls to this go in iterm2_print_user_vars.
    function iterm2_set_user_var
      printf "\033]1337;SetUserVar=%s=%s\007" "$argv[1]" (printf "%s" "$argv[2]" | base64)
    end

    # Users can override this.
    # It should call iterm2_set_user_var and produce no other output.
    function iterm2_print_user_vars
    end

    # iTerm2 inform terminal that command starts here
    function iterm2_precmd -e fish_postexec
      printf "\033]1337;RemoteHost=%s@%s\007\033]1337;CurrentDir=$PWD\007" $USER $iterm2_hostname
      iterm2_print_user_vars
    end

    function iterm2_prompt -e fish_prompt
      functions -e iterm2_fish_prompt
      functions -c fish_prompt iterm2_fish_prompt
      function fish_prompt
        set -l last_status $status
        iterm2_prompt_start
        # Restore the status
        sh -c "exit $last_status"
        iterm2_fish_prompt
        iterm2_prompt_end
      end
    end

    function restore_prompt -e fish_preexec
      funced -e cat iterm2_fish_prompt | sed 's|iterm2_fish_prompt|fish_prompt|' | source
      functions -e iterm2_fish_prompt
    end

    # If hostname -f is slow for you set iterm2_hostname before sourcing this script
    if not set -q iterm2_hostname
      set iterm2_hostname (hostname -f)
    end

    iterm2_precmd
    printf "\033]1337;ShellIntegrationVersion=1\007"
  end
end
